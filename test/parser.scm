(use-modules (oop goops)
	     (nnw core generic)
	     (nnw core view)
	     (nnw core view list-view)
	     (nnw core block)	     
	     (nnw core parser)
	     (sxml match)
	     (srfi srfi-1)
             (srfi srfi-64))

(test-begin "logs/parser")

(test-group "parse-text basic functionality"
  (let ((result (parse-text "line1\nline2\n\nline3")))
    (sxml-match-let (((view (@ (type ,tv)
			       (name ,nv))
		       (block (@ (type ,tb1)) (p ,line1))
		       (block (@ (type ,tb2)) (p ,line2))
		       (block (@ (type ,tb3)) (p ,line3)))
		     result))
		    (test-equal "first block content" "line1" line1)
		    (test-equal "second block content" "line2" line2)
		    (test-equal "third block content" "line3" line3)
		    (test-equal "first block type" "text" tb1)
		    (test-equal "second block type" "text" tb2)
		    (test-equal "third block type" "text" tb3)
		    (test-equal "View Type" "list-view" tv)
		    (test-equal "View Name" "Untitled Document" nv))))

(test-group "input->views+blocks functionality"
  (let* ((input-sxml '(view (@ (type "list-view") (name "Test List-View"))
                            (block (@ (type "text") (description "Block 1") (tags "tag1 tag2 tag3")) (p "First line"))
                            (block (@ (type "text") (description "Block 2") (tags "tag1 tag2 tag3")) (p "Second line"))))
         (result (input->views+blocks input-sxml)))
    (test-assert "result is a pair" (pair? result))
    (test-assert "car result is list of views" (list? (car result)))
    (test-assert "cdr result is list of blocks" (list? (cdr result)))
    (test-equal "has one view" 1 (length (car result)))
    (test-equal "has two blocks" 2 (length (cdr result)))
    (test-assert "view is list-view type" (is-a? (caar result) <list-view>))
    (test-equal "view's type is list-view " 'list-view (get-type (caar result)))
    (test-assert "view's content is not null" (not (null? (get-content (caar result)))))
    (test-equal "view name matches" "Test List-View" (get-name (caar result)))
    (test-assert "all blocks are text type" (every (lambda (b) (is-a? b <text>)) (cdr result)))
    (test-equal "block's tags match" '("tag1" "tag2" "tag3") (get-tags (cadr result)))))

(test-end "logs/parser")
